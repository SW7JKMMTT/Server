apply plugin: 'ear'

dependencies {
    deploy project(path: ":services", configuration: "archives")
}

allprojects  {
    apply plugin: 'checkstyle'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'jetty'
    apply plugin: 'java'

    group = 'rocks.stalin.sw708e16'
    version = '1.0.0'

    checkstyle {
        configFile = "${rootDir}/checkstyle.xml" as File
        toolVersion = 6.17
    }

    task checkstyle (type: Checkstyle) {

        description 'Run checkstyle'
        group 'verification'

        source 'src/main/java'
        include '**/*.java'
        ignoreFailures = true

        def configProps = ['proj.module.dir': projectDir.absolutePath]
        configProperties configProps

        // empty classpath
        classpath = files()

        reports {
            xml.enabled = true
        }
    }

    task report(type: Test) {
        reports {
            junitXml.enabled = true
            junitXml.destination = "$buildDir/reports/junit"
        }
        ignoreFailures = true

        File file = new File("exclusions.txt")
        if(file.exists()) {
            file.readLines().each {
                exclude "${it}"
            }
        }
    }
}

subprojects {

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    repositories {
        mavenLocal()

        mavenCentral()
    }

    dependencies {
        compile (group: 'org.jboss.resteasy', name: 'resteasy-jaxrs', version:'3.0.19.Final') {
            exclude(module: 'commons-httpclient')
            exclude(module: 'servlet-api')
        }
        compile group: 'org.jboss.resteasy', name: 'resteasy-spring', version:'3.0.19.Final'
        compileOnly group: 'org.jboss.resteasy', name: 'resteasy-jackson2-provider', version: '3.0.19.Final'
        compile group: 'org.springframework', name: 'spring-core', version:'4.3.4.RELEASE'
        compile group: 'org.springframework', name: 'spring-test', version:'4.3.4.RELEASE'
        compile group: 'org.springframework', name: 'spring-webmvc', version:'4.3.4.RELEASE'
        compile group: 'org.springframework', name: 'spring-tx', version:'4.3.4.RELEASE'
        compile group: 'org.springframework', name: 'spring-orm', version:'4.3.4.RELEASE'
        compile group: 'com.webcohesion.enunciate', name:'enunciate-core-annotations', version:'2.7.0'

        testCompile (group: 'junit', name: 'junit', version:'4.12') {
            exclude group: 'org.hamcrest'
        }
        testCompile 'org.hamcrest:hamcrest-library:1.3'
    }
}
